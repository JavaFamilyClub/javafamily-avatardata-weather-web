buildscript {
   ext {
      springVersion = '5.1.8.RELEASE'
      swaggerVersion = "2.9.2"
      jacksonVersion = '2.10.3'
      logbackVersion = '1.2.3'
      jupiterVersion = '5.6.2'
      jfUtilsVersion = '0.1.3-SNAPSHOT'
      jfRestVersion = '0.1.4-SNAPSHOT'
      jfWeatherVersion = '0.1.1-SNAPSHOT'
   }
}

plugins {
   id 'java'
   id 'maven-publish'
   id 'signing'
}

group = 'club.javafamily'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
   jcenter()
   mavenLocal()
   mavenCentral()
}

dependencies {
   // javafamily
   // weather
   implementation "club.javafamily:javafamily-avatardata-weather:${jfWeatherVersion}"

   // rest jfRestVersion
   implementation "club.javafamily:javafamily-rest:${jfRestVersion}"

   // utils
   implementation "club.javafamily:javafamily-utils:${jfUtilsVersion}"

   // spring
   implementation "org.springframework:spring-core:${springVersion}"
   implementation "org.springframework:spring-beans:${springVersion}"
   implementation "org.springframework:spring-context:${springVersion}"
   implementation "org.springframework:spring-context-support:${springVersion}"
   implementation "org.springframework:spring-web:${springVersion}"
   implementation "org.springframework:spring-webmvc:${springVersion}"

   // swagger
   implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
   implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

   // logging
   implementation "ch.qos.logback:logback-classic:${logbackVersion}"

   // test
   testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
   testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
   testImplementation "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
   testImplementation "org.springframework:spring-test:${springVersion}"
}


task sourcesJar(type: Jar) {
   from sourceSets.main.allSource
   archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
   from javadoc
   archiveClassifier = 'javadoc'
}

publishing {
   publications {
      mavenJava(MavenPublication) {
         groupId = project.group
         artifactId = project.name
         version = "${version}"
         from components.java
         artifact sourcesJar
         artifact javadocJar

         pom {
            name = 'JavaFamily Avatardata Weather Web Service'
            description = 'JavaFamily Avatardata Weather Service Web library'
            url = 'https://github.com/JavaFamilyClub/javafamily-avatardata-weather-web'
            licenses {
               license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
               }
            }
            developers {
               developer {
                  id = 'JavaFamilyClub'
                  name = 'JavaFamily'
                  email = 'javafamily.club@outlook.com'
                  organization = 'JavaFamily'
               }
            }
            scm {
               url = 'https://github.com/JavaFamilyClub/javafamily-avatardata-weather-web'
               connection = 'scm:git:git://github.com/JavaFamilyClub/javafamily-avatardata-weather-web.git'
               developerConnection = 'scm:git:ssh://git@github.com/JavaFamilyClub/javafamily-avatardata-weather-web.git'
            }
         }
      }
   }
   repositories {
      maven {
         def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
         def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

         url = project.version.endsWith('-SNAPSHOT') ? snapshotsUrl : releasesUrl

         credentials {
            username sonatypeUsername
            password sonatypePassword
         }
      }
   }
}

signing {
   sign publishing.publications.mavenJava
}

javadoc {
   options.addStringOption("charset", "UTF-8")
}

test {
   useJUnitPlatform()

   testLogging {
      events "passed", "skipped", "failed"
   }
}

wrapper {
   gradleVersion '6.2.1'
}
